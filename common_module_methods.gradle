ext {
    applyAndroidConfiguration = { module, nameSpace ->

        module.configure(module) {
            apply plugin: 'com.android.library'
            apply plugin: 'org.jetbrains.kotlin.android'
            apply plugin: 'kotlin-kapt'

            android {
                namespace = nameSpace
                compileSdk = compileSdkExt

                defaultConfig {
                    minSdk = minSdkExt
                    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    debug {
                        minifyEnabled = false
                        proguardFiles(
                                getDefaultProguardFile("proguard-android-optimize.txt"),
                                "proguard-rules.pro"
                        )
                    }

                    release {
                        minifyEnabled = true
                        proguardFiles(
                                getDefaultProguardFile("proguard-android-optimize.txt"),
                                "proguard-rules.pro"
                        )
                    }
                }

                compileOptions {
                    sourceCompatibility = javaVersionExt
                    targetCompatibility = javaVersionExt
                }

                kotlinOptions {
                    jvmTarget = jvmTargetExt
                }
            }
        }

        module.dependencies {
            implementation multidex
        }
    }

    applyDagger = { module ->
        module.dependencies {
            implementation dagger
            kapt daggerKapt
        }
    }

    applyCompose = { module, kotlinCompile ->
        module.configure(module) {

            android {
                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion = kotlinCompilerExtensionVersionExt
                }
            }
        }

        module.tasks.withType(kotlinCompile).configureEach {
            kotlinOptions {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }

        module.dependencies {
            implementation anndroidXComposeRuntime
            implementation anndroidXComposeUiWithVersions
            implementation anndroidXComposeFoundation
            implementation anndroidXComposeMaterial3WithVersion
            implementation(platform(anndroidXComposePlatform))
            implementation(anndroidXNavigation)
            implementation(coil)
            implementation(kotlinX)
        }
    }
}
